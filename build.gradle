plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id "com.google.protobuf" version "0.8.12"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

repositories {
     mavenLocal()
     mavenCentral()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.5.0'
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: '2.5.0'
    implementation group: 'com.typesafe', name: 'config', version: '1.4.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.4'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.12.1'
    implementation group: 'com.google.guava', name: 'guava', version:'29.0-jre'
    implementation group: 'io.opentelemetry', name: 'opentelemetry-sdk', version: '0.5.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.6.2'
    testImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: '2.5.0'
}

group 'com.rhysmccaig.kscheduler'
version '1.0.0-SNAPSHOT'

application {
    mainClassName = 'com.rhysmccaig.kscheduler.KScheduler'
}

checkstyle {
	toolVersion '8.33'
    configFile file("${project.rootDir}/config/checkstyle/google_checks.xml")
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'com/rhysmccaig/kscheduler/model/protos')
        }))
    }
}

test {
    useJUnitPlatform()
    enableAssertions = true
    finalizedBy jacocoTestReport
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.1'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/main/java']
        }
    }
}
